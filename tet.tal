( untitled block game - a wip by nf@wh3rd.net )

( macros )

%MOD   { DIVk MUL SUB }
%debug { #00 .System/debug DEO }
%halt  { #81 .System/halt DEO }

%well_screen_x { #0058 }
%well_screen_y { #0040 }
%next_screen_x { #00b8 }
%next_screen_y { #00b8 }

%well_line_start { #03 }
%well_line_end { #0c }
%well_first_line { #0040 }
%well_last_line { #0170 }
%well_first_block_minus_1 { #0042 }

( devices )

|00 @System     [ &vector $2 &wst      $1 &rst    $1 &pad   $4 &r      $2 &g      $2 &b    $2 &debug  $1 &halt $1 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 &error  $1 ]
|20 @Screen     [ &vector $2 &width    $2 &height $2 &auto  $1 &pad    $1 &x      $2 &y    $2 &addr   $2 &pixel $1 &sprite $1 ]
|30 @Audio0     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr $2 &volume $1 &pitch $1 ]
|80 @Controller [ &vector $2 &button   $1 &key    $1 ]
|c0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour  $1 &minute $1 &second $1 &dotw $1 &doty   $2 &isdst $1 ]

( variables )

|0000

( the piece controlled by the player )
@cur  &type $1 &rot $1 &x $1 &y $1 &vec $2
( a virtual piece used to cur piece movement )
@test &type $1 &rot $1 &x $1 &y $1 &vec $2 &blocked $1
( the next piece to enter the field )
@next &type $1 &rot $1 &x $1 &y $1 &vec $2

@debounce   $1
@timer      &tick $1 &speed $1
@is-compact $1

( program )

|0100 ( -> )

	( theme ) 
	#0ea5 .System/r DEO2 
	#0cf2 .System/g DEO2
	#09d9 .System/b DEO2

	( vectors )
	;on-frame .Screen/vector DEO2
	;on-button .Controller/vector DEO2

	( resize )
	#0100 .Screen/width DEO2
	#0100 .Screen/height DEO2

	#20 .timer/speed STZ

	draw-bg
	init-well
	new-cur-piece
	set-cur-piece
	set-next-piece
BRK

@on-frame ( -> )
	( compact if we have a reason to )
	.is-compact LDZ
		,&p-compact JCN
	compact-well
	( remember the result, avoid duplicate work )
	DUP .is-compact STZ 
		,&p-compact JCN
	( compcat mutated, so re-set the piece which was wiped )
	set-cur-piece
	&p-compact

	.cur/type LDZ .test/type STZ
	.cur/rot LDZ .test/rot STZ
	.cur/y LDZ .test/y STZ
	.cur/x LDZ .test/x STZ

	( inputs )
	.Controller/button DEI
	#01 pressed ,&p-a JCN
		.test/rot LDZ #03 ADD #04 MOD .test/rot STZ
		&p-a
	#02 pressed ,&p-b JCN
		.test/rot LDZ INC #04 MOD .test/rot STZ
		&p-b
	#40 pressed ,&p-lt JCN
		.test/x LDZ #01 SUB .test/x STZ
		&p-lt
	#80 pressed ,&p-rt JCN
		.test/x LDZ INC .test/x STZ
		&p-rt 
	POP

	( test x/rot change )
	.test/rot LDZ .cur/rot LDZ EQU
	.test/x LDZ .cur/x LDZ EQU
		AND ,&x-done JCN
	try-test-piece
		,&x-blocked JCN
	unset-cur-piece
	.test/rot LDZ .cur/rot STZ
	.test/x LDZ .cur/x STZ
	set-cur-piece
		,&x-done JMP
	&x-blocked
	.cur/rot LDZ .test/rot STZ
	.cur/x LDZ .test/x STZ
	&x-done

	#00 ( inc-y? )

	( gravity )
	.timer/tick LDZ INC
	.timer/speed LDZ MOD
	DUP .timer/tick STZ
		,&p-tick JCN
	INC ( gravity applied )
	&p-tick

	( down button )
	.Controller/button DEI #20 AND #20 EOR
		,&p-dn JCN
	INC ( down button pressed )
	&p-dn

	( test y change )
	#01 LTH
		,&y-done JCN
	.cur/y LDZ INC .test/y STZ
	try-test-piece
		,&y-blocked JCN
	unset-cur-piece
	.test/y LDZ .cur/y STZ
	set-cur-piece
		,&y-done JMP
	&y-blocked
	( freeze the current piece, create a new piece )
	freeze-cur-piece
	new-cur-piece
	set-cur-piece
	set-next-piece
	( there are new frozen blocks, compact next frame )
	#00 .is-compact STZ
	&y-done

	draw-well

	draw-next
BRK

@on-button ( -> )
	.Controller/button DEI .debounce LDZ AND .debounce STZ
BRK

@pressed ( button code -- not-pressed )
	DUP2 AND ,&held JCN ,&no JMP 
	&held
		DUP .debounce LDZ AND ,&no JCN
		DUP .debounce LDZ ORA .debounce STZ
		POP #00 JMP2r
	&no
		POP #01 JMP2r

@draw-bg ( -- )
	draw-bg-textures
	draw-bg-boxes

	next_screen_x #0001 ADD2
	next_screen_y #0002 ADD2 
	;strings/next #01 draw-str
JMP2r

@draw-bg-boxes ( -- )
	( main field )
	well_screen_x NIP #08 SUB 
	well_screen_y NIP #08 SUB 
	#58 #a8 draw-box 

	( next piece )
	next_screen_x NIP #08 SUB
	next_screen_y NIP #08 SUB
	#30 #30 draw-box
JMP2r

@draw-bg-textures ( -- )
	;bg/stripes .Screen/addr DEO2
	#f2 .Screen/auto DEO
	#0000 .Screen/x DEO2
	&bg-x
		.Screen/x DEI2 #0100 GTH2
			,&bg-done JCN
		#0000 .Screen/y DEO2
		&bg-y
			#03 .Screen/sprite DEO
			.Screen/y DEI2 #0100 LTH2 
				,&bg-y JCN
		.Screen/x DEI2 #0080 ADD2 .Screen/x DEO2
		,&bg-x JMP
	&bg-done

	;bg/dots .Screen/addr DEO2
	#01 .Screen/auto DEO
	well_screen_x .Screen/x DEO2
	well_screen_y .Screen/y DEO2
	#00 &well-y
		INC 
		DUP #07 MOD #00 EQU #70 SFT 
		OVR #09 MOD #42 SFT 
		ORA #03 ORA .Screen/sprite DEO
		DUP #c8 GTH
			,&well-done JCN
		DUP #0a MOD
			,&well-y JCN
		well_screen_x .Screen/x DEO2
		.Screen/y DEI2 #0008 ADD2 .Screen/y DEO2
			,&well-y JMP
	&well-done POP

	next_screen_x .Screen/x DEO2
	next_screen_y .Screen/y DEO2
	#00 &next-y
		INC 
		DUP #04 MOD #02 AND #71 SFT 
		OVR #07 MOD #41 SFT ORA
		#00 ORA .Screen/sprite DEO
		DUP #18 GTH
			,&next-done JCN
		DUP #05 MOD
			,&next-y JCN
		next_screen_x .Screen/x DEO2
		.Screen/y DEI2 #0008 ADD2 .Screen/y DEO2
			,&next-y JMP
	&next-done POP
JMP2r

@draw-box ( x y w h -- )
	( corners )
	;border-chr/corner .Screen/addr DEO2
	#00 .Screen/auto DEO
	( tl ) 
	OVR2 #00 SWP .Screen/y DEO2 #00 SWP .Screen/x DEO2
	#8a .Screen/sprite DEO
	( tr )
	SWP2k SWP2 #ff00 AND2 ADD2 #00 SWP .Screen/y DEO2 #00 SWP .Screen/x DEO2
	#9a .Screen/sprite DEO
	( bl )
	SWP2k SWP2 #00ff AND2 ADD2 #00 SWP .Screen/y DEO2 #00 SWP .Screen/x DEO2
	#aa .Screen/sprite DEO
	( br )
	SWP2k SWP2 ADD2 #00 SWP .Screen/y DEO2 #00 SWP .Screen/x DEO2
	#ba .Screen/sprite DEO

	( edges )
	;border-chr/edge-ver .Screen/addr DEO2
	#02 .Screen/auto DEO
	( lt )
	OVR2 #08 ADD #00 SWP .Screen/y DEO2 #00 SWP .Screen/x DEO2
	DUP #08 SUB &lt-loop
		#8a .Screen/sprite DEO
		#08 SUB DUP
			,&lt-loop JCN
	POP
	( rt )
	SWP2k SWP2 #ff00 AND2 ADD2 #08 ADD #00 SWP .Screen/y DEO2 #00 SWP .Screen/x DEO2
	DUP #08 SUB &rt-loop
		#9a .Screen/sprite DEO
		#08 SUB DUP
			,&rt-loop JCN
	POP
	;border-chr/edge-hor .Screen/addr DEO2
	#01 .Screen/auto DEO
	( tp ) 
	OVR2 #00 SWP .Screen/y DEO2 #08 ADD #00 SWP .Screen/x DEO2
	OVR #08 SUB &tp-loop
		#8a .Screen/sprite DEO
		#08 SUB DUP
			,&tp-loop JCN
	POP
	( bt )
	SWP2k SWP2 #00ff AND2 ADD2 #00 SWP .Screen/y DEO2 #08 ADD #00 SWP .Screen/x DEO2
	OVR #08 SUB &bt-loop
		#aa .Screen/sprite DEO
		#08 SUB DUP
			,&bt-loop JCN
	POP
	POP2 POP2
JMP2r

@init-well ( -- )
	;well &loop
		DUP2 #0ff0 AND2 well_last_line GTH2
			,&block JCN
		DUP #0f AND well_line_start LTH
			,&block JCN
		DUP #0f AND well_line_end GTH
			,&block JCN
		&space #00
			,&store JMP
		&block #02
		&store ROT ROT STAk ROT POP
		INC2 DUP2 ;well/end LTH2
			,&loop JCN
	POP2
JMP2r

@draw-well ( -- )
	well_screen_x .Screen/x DEO2
	well_screen_y .Screen/y DEO2
	#01 .Screen/auto DEO
	well_first_block_minus_1 ;well ADD2 &loop
		INC2
		( check eol )
		DUP #0f AND well_line_end GTH
			,&eol JCN
		( check bottom )
		DUP2 #0ff0 AND2 well_last_line GTH2
			,&done JCN
		( draw blank or block? )
		DUP2 LDA DUP ,&draw-block JCN
		&draw-blank
			;blank-chr .Screen/addr DEO2
			#40 ORA .Screen/sprite DEO
				,&loop JMP
		&draw-block
			;block-icn .Screen/addr DEO2
			#c0 ORA .Screen/sprite DEO
				,&loop JMP
		&eol
			well_screen_x .Screen/x DEO2
			#0008 .Screen/y DEI2 ADD2 .Screen/y DEO2
			#0005 ADD2
			,&loop JMP
	&done POP2
JMP2r

@draw-next ( -- )
	next_screen_x #0004 ADD2 .Screen/x DEO2
	next_screen_y #0004 ADD2 .Screen/y DEO2
	#01 .Screen/auto DEO
	;well-next #0001 SUB2 &loop
		INC2
		( check eol )
		DUP #0f AND #04 GTH
			,&eol JCN
		( check bottom )
		DUP #f0 AND #40 GTH
			,&done JCN
		( draw blank or block? )
		DUP2 LDA DUP ,&draw-block JCN
		&draw-blank
			;blank-chr .Screen/addr DEO2
			#40 ORA .Screen/sprite DEO
				,&loop JMP
		&draw-block
			;block-icn .Screen/addr DEO2
			#c0 ORA .Screen/sprite DEO
				,&loop JMP
		&eol
			next_screen_x #0004 ADD2 .Screen/x DEO2
			#0008 .Screen/y DEI2 ADD2 .Screen/y DEO2
			#000a ADD2
			,&loop JMP
	&done POP2
JMP2r

@compact-well ( -- noop )
	#00 well_first_block_minus_1 ;well ADD2 &loop
		INC2
		( check eol )
		DUP #0f AND well_line_end GTH
			,&eol JCN
		( check bottom )
		DUP2 #0ff0 AND2 well_last_line GTH2
			,&done JCN
		( sum elements )
		LDAk OVR2 POP ADD SWP2 NIP ROT
			,&loop JMP
		&eol
			ROT
			DUP #14 EQU
				,&hl-line JCN
			DUP #28 EQU
				,&rem-line JCN
			POP
			#00 ROT ROT
			#0005 ADD2
				,&loop JMP
			&hl-line 
				POP
				DUP2 #000a SUB2
				&hl-block
					DUP2 #04 ROT ROT STA
					INC2 GTH2k
						,&hl-block JCN
				POP2 POP2 #00
				JMP2r
			&rem-line
				POP
				remove-well-line
				#00
				JMP2r
	&done POP2 POP #01
JMP2r

@remove-well-line ( well-addr-on-line -- )
	&loop
		#0001 SUB2
		( read block from line above )
		DUP2k #0010 SUB2 LDA
		( exclude current piece )
		#fe AND 
		( store block in current line )
		ROT ROT STA
		( see if we're done )
		DUP2 #0fff AND2 #0010 GTH2
			,&loop JCN
	POP2
JMP2r

@new-cur-piece ( -- )
	#06 .cur/x STZ
	#00 .cur/y STZ
	#00 .cur/rot STZ
	.next/type LDZ .cur/type STZ

	#00 .next/x STZ
	#01 .next/y STZ
	.next/type LDZ INC #07 MOD .next/type STZ
JMP2r

@set-cur-piece ( -- )
	;&vec .cur/vec STZ2
	.cur each-piece-block
JMP2r
	&vec ( well-offset -- )
		;well ADD2 #01 ROT ROT STA
	JMP2r

@unset-cur-piece ( -- )
	;&vec .cur/vec STZ2
	.cur each-piece-block
JMP2r
	&vec ( well-offset -- )
		;well ADD2 LDAk #fe AND ROT ROT STA
	JMP2r

@freeze-cur-piece ( -- )
	;&vec .cur/vec STZ2
	.cur each-piece-block
JMP2r
	&vec ( well-offset -- )
		;well ADD2 #02 ROT ROT STA
	JMP2r

( reports whether .test can fit )
@try-test-piece ( -- blocked )
	#00 .test/blocked STZ
	;&vec .test/vec STZ2
	.test each-piece-block
	.test/blocked LDZ
JMP2r
	&vec ( well-offset -- )
		;well ADD2 LDA #02 AND .test/blocked LDZ ORA .test/blocked STZ
	JMP2r

@set-next-piece ( -- )
	;well-next &clear
		DUP2 #00 ROT ROT STA
	INC2 DUP2 ;well-next/end LTH2
		,&clear JCN
	POP2

	;&vec .next/vec STZ2
	.next each-piece-block
JMP2r
	&vec ( well-offset -- )
		;well-next ADD2 #01 ROT ROT STA
	JMP2r

@each-piece-block ( piece -- )
	DUP
	LDZk #30 SFT ( type ) 
	SWP INC LDZ #10 SFT ( rot )
	ADD #00 SWP 
	;pieces ADD2 LDA2 ( piece16 )
	#33 ( go backward from bottom-right )
	#01 ( col-count, 0-3 )
	SWP2 ( piece well-idx col-count piece16 )

	&loop

	( test whether a piece block is at well-idx )
	DUP #01 AND ,&ok JCN ,&p-ok JMP
	&ok 
		( stash piece16 and arrange to call piece/vec )
		STH2 SWP ROT ( col-count well-idx piece )
		( well-idx piece -- piece well-offset )
		DUP2 SWP OVR
		INC INC INCk LDZ SWP LDZ SWP
		#00 SWP #0010 MUL2
		ROT #00 SWP ADD2
		ROT #00 SWP ADD2
		( piece well-offset -- well-offset piece/vec )
		ROT #04 ADD LDZ2
		( call piece/vec )
		JSR2
		( restore stack )
		SWP ROT STH2r ( piece well-idx col-count piece16 )
	&p-ok

	SWP2 ( piece piece16 well-idx col-count )
	( col-count %= 4 )
	#04 MOD
	( if col-count == 0, move to next row )
	SWP OVR ,&inc JCN #0c SUB
	&inc 
	( decrement well-idx )
	#01 SUB SWP
	( increment col-count )
	INC
	SWP2 ( piece well-idx col-count piece16 )

	( shift piece16 right, and stop if no more blocks )
	#01 SFT2 DUP2 #0000 GTH2
		,&loop JCN

	POP2 POP2 POP
JMP2r

@draw-str ( x* y* text* color -- )

	#01 .Screen/auto DEO
	STH
	SWP2 .Screen/y DEO2
	SWP2 .Screen/x DEO2
	&loop
		LDAk #20 SUB #00 SWP #30 SFT2 ;font ADD2 .Screen/addr DEO2
		STHkr .Screen/sprite DEO
		INC2 LDAk ?&loop
	POP2
	STHr POP
	#00 .Screen/auto DEO

JMP2r

( assets )

@strings
	&lines "LINES $1
	&next "NEXT $1
@pieces
	&i 0f00 4444 0f00 4444
	&s 4620 6c00 4620 6c00
	&z 2640 c600 2640 c600
	&l 4460 0e80 c440 2e00
	&j 6440 e200 44c0 08e0
	&t 4640 0e40 4c40 4e00
	&b 6600 6600 6600 6600

@blank-chr
	ffff ffff ffff ffff
@block-icn
	7e81 8181 8181 817f
	007f 7f7f 7f7f 7f7f

@border-chr
	&corner
	0000 0000 0003 0707
	0000 0000 0304 0808
	&edge-ver
	0707 0707 0707 0707
	0808 0808 0808 0808
	&edge-hor
	0000 0000 00ff ffff
	0000 0000 ff00 0000
@bg
	&stripes
	030c 30c0 030c 30c0
	e38f 3ef8 e38f 3ef8
	&dots
	0000 4000 0000 1000
	0000 4000 0000 0000

( game data )

|4000

@well $200 &end
@well-next $40 &end

@font ( atari8.uf1 )
	0000 0000 0000 0000 6060 6060 6000 6000
	6666 6600 0000 0000 006c fe6c 6cfe 6c00
	183e 603c 067c 1800 0066 6c18 3066 4600
	386c 3870 decc 7600 6060 6000 0000 0000
	0e1c 1818 181c 0e00 7038 1818 1838 7000
	0066 3cff 3c66 0000 0018 187e 1818 0000
	0000 0000 0030 3060 0000 007e 0000 0000
	0000 0000 0018 1800 0206 0c18 3060 4000
	&num
	3c66 6e76 6666 3c00 1838 1818 1818 7e00
	3c66 060c 1830 7e00 7e0c 180c 0666 3c00
	0c1c 3c6c 7e0c 0c00 7e60 7c06 0666 3c00
	3c60 607c 6666 3c00 7e06 0c18 3030 3000
	3c66 663c 6666 3c00 3c66 663e 060c 3800
	0060 6000 6060 0000 0030 3000 3030 6000
	0c18 3060 3018 0c00 0000 7e00 007e 0000
	6030 180c 1830 6000 3c66 060c 1800 1800
	3c66 6e6a 6e60 3e00 183c 6666 7e66 6600
	7c66 667c 6666 7c00 3c66 6060 6066 3c00
	786c 6666 666c 7800 7e60 607c 6060 7e00
	7e60 607c 6060 6000 3e60 606e 6666 3e00
	6666 667e 6666 6600 7830 3030 3030 7800
	0606 0606 0666 3c00 666c 7870 786c 6600
	6060 6060 6060 7e00 c6ee fed6 c6c6 c600
	6676 7e7e 6e66 6600 3c66 6666 6666 3c00
	7c66 667c 6060 6000 3c66 6666 766c 3600
	7c66 667c 6c66 6600 3c66 603c 0666 3c00
	7e18 1818 1818 1800 6666 6666 6666 3e00
	6666 6666 663c 1800 c6c6 c6d6 feee c600
	6666 3c18 3c66 6600 6666 663c 1818 1800
	7e06 0c18 3060 7e00 7860 6060 6060 7800
