( untitled block game - a wip by nf@wh3rd.net )

( macros )

%MOD   { DIVk MUL SUB }
%debug { #00 .System/debug DEO }
%halt  { #81 .System/halt DEO }

( devices )

|00 @System     [ &vector $2 &wst      $1 &rst    $1 &pad   $4 &r      $2 &g      $2 &b    $2 &debug  $1 &halt $1 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 &error  $1 ]
|20 @Screen     [ &vector $2 &width    $2 &height $2 &auto  $1 &pad    $1 &x      $2 &y    $2 &addr   $2 &pixel $1 &sprite $1 ]
|30 @Audio0     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr $2 &volume $1 &pitch $1 ]
|80 @Controller [ &vector $2 &button   $1 &key    $1 ]
|c0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour  $1 &minute $1 &second $1 &dotw $1 &doty   $2 &isdst $1 ]

( variables )

|0000

@cur  &type $1 &rot $1 &x $1 &y $1 &vec $2
@next &type $1 &rot $1 &x $1 &y $1 &vec $2 &blocked $1

@debounce $1
@timer    &tick $1 &speed $1

( program )

|0100 ( -> )

	( theme ) 
	#0fa5 .System/r DEO2 
	#0df2 .System/g DEO2
	#0ad9 .System/b DEO2

	( vectors )
	;on-frame .Screen/vector DEO2
	;on-button .Controller/vector DEO2

	( resize )
	#0100 .Screen/width DEO2
	#0100 .Screen/height DEO2

	#20 .timer/speed STZ
	#07 .cur/x STZ
	#03 .cur/y STZ

	draw-bg
	init-well
	set-cur-piece
BRK

@on-frame ( -> )

	.cur/type LDZ .next/type STZ
	.cur/rot LDZ .next/rot STZ
	.cur/y LDZ .next/y STZ
	.cur/x LDZ .next/x STZ

	( inputs )
	.Controller/button DEI
	#02 pressed ,&p-b JCN
		.next/rot LDZ INC #04 MOD .next/rot STZ
		&p-b
	#40 pressed ,&p-lt JCN
		.next/x LDZ #01 SUB .next/x STZ
		&p-lt
	#80 pressed ,&p-rt JCN
		.next/x LDZ INC .next/x STZ
		&p-rt 
	POP

	( test x/rot change )
	.next/rot LDZ .cur/rot LDZ EQU 
	.next/x LDZ .cur/x LDZ EQU 
		AND ,&x-done JCN
	test-next-piece
		,&x-blocked JCN
	unset-cur-piece
	.next/rot LDZ .cur/rot STZ
	.next/x LDZ .cur/x STZ
	set-cur-piece
		,&x-done JMP
	&x-blocked
	.cur/rot LDZ .next/rot STZ
	.cur/x LDZ .next/x STZ
	&x-done

	#00 ( inc-y? )
	( gravity )
	.timer/tick LDZ INC
	.timer/speed LDZ MOD
	DUP .timer/tick STZ
		,&p-tick JCN
	INC
	&p-tick
	( down button )
	.Controller/button DEI #20 AND #20 EOR ,&p-dn JCN
		INC
	&p-dn
	( test y change )
	#01 LTH
		,&y-done JCN
	.cur/y LDZ INC .next/y STZ
	test-next-piece
		,&y-blocked JCN
	unset-cur-piece
	.next/y LDZ .cur/y STZ
	set-cur-piece
		,&y-done JMP
	&y-blocked
	freeze-cur-piece
	new-cur-piece
	&y-done

	draw-well
BRK

@on-button ( -> )
	.Controller/button DEI .debounce LDZ AND .debounce STZ
BRK

@pressed ( button code -- not-pressed )
	DUP2 AND ,&held JCN ,&no JMP 
	&held
		DUP .debounce LDZ AND ,&no JCN
		DUP .debounce LDZ ORA .debounce STZ
		POP #00 JMP2r
	&no
		POP #01 JMP2r

@draw-bg ( -- )
	;bg/stripes .Screen/addr DEO2
	#92 .Screen/auto DEO
	#0000 .Screen/x DEO2
	&loop-x
		.Screen/x DEI2 #0100 GTH2
			,&loop-done JCN
		#0000 .Screen/y DEO2
		&loop-y
			#03 .Screen/sprite DEO
			.Screen/y DEI2 #0100 LTH2 
				,&loop-y JCN
		.Screen/x DEI2 #0050 ADD2 .Screen/x DEO2
		,&loop-x JMP
	&loop-done
	;bg/dots .Screen/addr DEO2
	#92 .Screen/auto DEO
	#0058 .Screen/x DEO2
	#0038 .Screen/y DEO2
	#00
	&loop-d
		INC #01 AND DUP #40 SFT 
		#03 ORA
		.Screen/sprite DEO
		.Screen/y DEI2 #00d8 LTH2
			,&loop-d JCN
	POP

	%draw_edge { #00 SWP .Screen/y DEO2 #00 SWP .Screen/x DEO2 .Screen/sprite DEO }

	( corners )
	;border-chr/corner .Screen/addr DEO2
	#00 .Screen/auto DEO
	( tl ) #8a #50 #30 draw_edge
	( tr ) #9a #a8 #30 draw_edge
	( bl ) #aa #50 #d8 draw_edge
	( br ) #ba #a8 #d8 draw_edge

	( edges )
	;border-chr/edge-ver .Screen/addr DEO2
	#91 .Screen/auto DEO
	( lt ) #8a #50 #38 draw_edge
	       #8a #50 #88 draw_edge
	( rt ) #9a #a8 #38 draw_edge
	       #9a #a8 #88 draw_edge
	;border-chr/edge-hor .Screen/addr DEO2
	#92 .Screen/auto DEO
	( tp ) #8a #58 #30 draw_edge
	( bt ) #aa #58 #d8 draw_edge
JMP2r

@init-well ( -- )
	;well &loop
		DUP2 #0ff0 AND2
		DUP2 #0170 GTH2 ,&skip JCN
		POP2 DUP2 #000f AND2
		DUP #04 LTH ,&skip JCN
		DUP #0d GTH ,&skip JCN
		POP2 DUP2 #00 ,&set JMP
		&skip POP2 DUP2 #02 
		&set ROT ROT STA
	INC2 DUP2 ;well/end LTH2 ,&loop JCN POP2
JMP2r

@draw-well ( -- )
	#0058 .Screen/x DEO2
	#0038 .Screen/y DEO2
	#01 .Screen/auto DEO
	#0043 ;well ADD2 &loop
		INC2
		( check eol )
		DUP #0f AND #0d GTH
			,&eol JCN
		( check bottom )
		DUP2 #0ff0 AND2 #0170 GTH2
			,&done JCN
		( draw blank or block? )
		DUP2 LDA DUP ,&draw-block JCN
		&draw-blank
			;blank-chr .Screen/addr DEO2
			#40 ORA .Screen/sprite DEO
				,&loop JMP
		&draw-block
			;block-icn .Screen/addr DEO2
			#c0 ORA .Screen/sprite DEO
				,&loop JMP
		&eol
			#0058 .Screen/x DEO2
			#0008 .Screen/y DEI2 ADD2 .Screen/y DEO2
			#0005 ADD2
			,&loop JMP
	&done POP2
JMP2r

@new-cur-piece ( -- )
	#07 .cur/x STZ
	#00 .cur/y STZ
	#00 .cur/rot STZ
	.cur/type LDZ INC #05 MOD .cur/type STZ
JMP2r

@set-cur-piece ( -- )
	;&vec .cur/vec STZ2
	.cur each-piece-block
JMP2r
	&vec ( well-addr -- )
		#01 ROT ROT STA
	JMP2r

@unset-cur-piece ( -- )
	;&vec .cur/vec STZ2
	.cur each-piece-block
JMP2r
	&vec ( well-addr -- )
		LDAk #fe AND ROT ROT STA
	JMP2r

@freeze-cur-piece ( -- )
	;&vec .cur/vec STZ2
	.cur each-piece-block
JMP2r
	&vec ( well-addr -- )
		#02 ROT ROT STA
	JMP2r

@test-next-piece ( -- blocked )
	#00 .next/blocked STZ
	;&vec .next/vec STZ2
	.next each-piece-block
	.next/blocked LDZ
JMP2r
	&vec ( well-addr -- )
		LDA #02 AND .next/blocked LDZ ORA .next/blocked STZ
	JMP2r

@each-piece-block ( piece -- )
	DUP
	LDZk #30 SFT ( type ) 
	SWP INC LDZ #10 SFT ( rot )
	ADD #00 SWP 
	;pieces ADD2 LDA2 ( piece16 )
	#33 ( go backward from bottom-right )
	#01 ( col-count, 0-3 )
	SWP2 ( piece well-idx col-count piece16 )

	&loop

	( test whether a piece block is at well-idx )
	DUP #01 AND ,&ok JCN ,&p-ok JMP
	&ok 
		( stash piece16 and arrange to call piece/vec )
		STH2 SWP ROT ( col-count well-idx piece )
		( well-idx piece -- piece well-addr )
		DUP2 SWP OVR
		INC INC INCk LDZ SWP LDZ SWP
		#00 SWP #0010 MUL2
		ROT #00 SWP ADD2
		;well ADD2
		ROT #00 SWP ADD2
		( piece well-addr -- well-addr piece/vec )
		ROT #04 ADD LDZ2
		( call piece/vec )
		JSR2
		( restore stack )
		SWP ROT STH2r ( piece well-idx col-count piece16 )
	&p-ok

	SWP2 ( piece piece16 well-idx col-count )
	( col-count %= 4 )
	#04 MOD
	( if col-count == 0, move to next row )
	SWP OVR ,&inc JCN #0c SUB
	&inc 
	( decrement well-idx )
	#01 SUB SWP
	( increment col-count )
	INC
	SWP2 ( piece well-idx col-count piece16 )

	( shift piece16 right, and stop if no more blocks )
	#01 SFT2 DUP2 #0000 GTH2
		,&loop JCN

	POP2 POP2 POP
JMP2r

( assets )

@pieces
	&i 0f00 4444 0f00 4444
	&s 4620 6c00 4620 6c00
	&l 4460 0e80 c440 2e00
	&t 0e40 4c40 4e00 4640
	&b 6600 6600 6600 6600

@blank-chr
	ffff ffff ffff ffff
@block-icn
	7e81 8181 8181 817f
	007f 7f7f 7f7f 7f7f

@border-chr
	&corner
	0000 0000 0003 0707
	0000 0000 0304 0808
	&edge-ver
	0707 0707 0707 0707
	0808 0808 0808 0808
	&edge-hor
	0000 0000 00ff ffff
	0000 0000 ff00 0000
@bg
	&stripes
	030c 30c0 030c 30c0
	e38f 3ef8 e38f 3ef8
	&dots
	0008 4000 0020 0100

( game data )

|4000

@well $200 &end
