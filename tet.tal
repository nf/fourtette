(

fourtette, a falling block game by nf@wh3rd.net

Technical notes:

The playable well is 10 columns by 40 rows, as per the guideline. It is stored
in a x10 by x30 region of memory starting at @well. Each row is x10 bytes, with
the playable well in columns (x03, x0c), so the three bytes at the start and
end of each line are not usable and form the walls.

The high nibble of each well byte indicates the block state (for gameplay
purposes), the low nibble indicates the block type (for drawing purposes).

 [ 8 4 2 1 8 4 2 1 ]
   | | | | |  \|/
   | | | | |   type (0-6 normal pieces, 7 ghost)
   | | | | unused
   | | | falling  - set by set-cur-piece
   | | frozen     - set by freeze-cur-piece
   | compacting   - set and cleared by compact-well
   wall/bottom    - set by init-well

)

( macros )

%MOD   { DIVk MUL SUB }
%debug { #f4 .System/debug DEO }
%halt  { #81 .System/halt DEO }

( devices )

|00 @System     [ &vector $2 &wst      $1 &rst    $1 &pad   $4 &r      $2 &g      $2 &b    $2 &debug  $1 &halt $1 ]
|10 @Console    [ &vector $2 &read     $1 &pad    $5 &write $1 &error  $1 ]
|20 @Screen     [ &vector $2 &width    $2 &height $2 &auto  $1 &pad    $1 &x      $2 &y    $2 &addr   $2 &pixel $1 &sprite $1 ]
|30 @Audio0     [ &vector $2 &position $2 &output $1 &pad   $3 &adsr   $2 &length $2 &addr $2 &volume $1 &pitch $1 ]
|80 @Controller [ &vector $2 &button   $1 &key    $1 ]
|c0 @DateTime   [ &year   $2 &month    $1 &day    $1 &hour  $1 &minute $1 &second $1 &dotw $1 &doty   $2 &isdst $1 ]

( variables )

|0000

|0010

( the piece controlled by the player )
@cur   &type $1 &rot $1 &x $1 &y $1
( a virtual piece used to cur piece movement )
@test  &type $1 &rot $1 &x $1 &y $1
( a virtual piece that sits where cur would drop )
@ghost &type $1 &rot $1 &x $1 &y $1
( the next pieces to enter the field )
@next $5
( the hold piece )
@hold &type $1 &ok $1

@score
	&level $1
	&lines $2
	&points $2

@music-state &counter $1 &position $2

( program )

|0100 ( -> )
	init-prng

	;button/vector .Controller/vector DEO2

	#0100 .Screen/width DEO2
	#0100 .Screen/height DEO2
	;theme/mono theme/set

	init-title
BRK

@init-title ( -- )
	;on-frame-title .Screen/vector DEO2
	#00 ;on-frame-title/n STA
	music/init
JMP2r

@on-frame-title ( -> )
	[ LIT &n $1 ] #84 EQU
		?&anim-done
	;costab #00 ,&n LDR ADD2 LDA ,&yoffs STR
	,&n LDR INC ,&n STR
	( clear )
	#0000 .Screen/x DEO2 #0000 .Screen/y DEO2 #83 .Screen/pixel DEO
	( draw image )
	;title-image #0010 #0020 #00 [ LIT &yoffs $1 ] SUB2 draw-image
		!&draw-done
	&anim-done

	( play music )
	music/on-frame

	( draw "press start" )
	#0054 #00e8 ;strings/start 
	#0d [ LIT &blink $1 ] #b0 LTH #03 MUL SUB draw-str
	,&blink LDR INC ,&blink STR

	&draw-done

	#0c button/pressed ?&start
BRK
	&start
	init-game
BRK

@init-game ( -- )
	;on-frame-game .Screen/vector DEO2
	#00 .score/level STZ
	#0000 .score/lines STZ2
	#0000 .score/points STZ2
	init-well
	init-level
	init-pieces
	draw-bg
JMP2r

@on-frame-game ( -> )
	( press start to pause )
	#08 button/pressed #00 EQU
		?&no-pause
	,&is-paused LDR #01 EOR ,&is-paused STR
	&no-pause

	[ LIT &is-paused $1 ]
		?&draw

	.ghost clear-piece
	.cur clear-piece

	[ LIT &is-compact $1 ]
		?&no-compact
	compact-well ,&is-compact STR 
		!&update
	&no-compact
	
	( swap hold piece )
	#04 button/pressed #00 EQU
		?&no-hold
	( only allow one swap per freeze )
	[ LIT &hold-swapped $1 ]
		?&no-hold
	( put cur piece in hold )
	#01 ,&hold-swapped STR
	[ LIT &not-held 01 ]
		?&first-hold
	( swap hold and cur )
	;sfx-vol/hold LDA ;sfx/hold play-sample
	init-cur-pos
	.cur/type LDZ .hold/type LDZ .cur/type STZ .hold/type STZ
	#00 ;&freeze-timer STA
		!&update
	( no piece held yet )
	&first-hold
	;sfx-vol/hold LDA ;sfx/hold play-sample
	#00 ,&not-held STR 
	.cur/type LDZ .hold/type STZ
	#01 .hold/ok STZ
	#00 ;&freeze-timer STA
		!&make-new-piece
	&no-hold

	.test .cur copy-piece

	( handle rot change )
	.cur/type LDZ #03 EQU ( b can't rotate )
		?&rot-done
	#01 button/pressed #03 MUL
	#02 button/pressed 
	ADD
	DUP #03 EQU ,&rot-anti STR
	.cur/rot LDZ ADD #04 MOD
	DUP .test/rot STZ .cur/rot LDZ EQU
		?&rot-done
	( try in place )
	.test try-piece #00 EQU
		?&rot-ok
	( try kicks )
	[ LIT &rot-anti $1 ] try-kick
		?&rot-done
	( rot changed )
	&rot-ok
	;sfx-vol/rot LDA ;sfx/rot play-sample
	.cur .test copy-piece
	#00 ;&freeze-timer STA
	&rot-done

	( handle x change )
	#80 button/pressed ( right )
	#40 button/pressed SUB ( left )
	DUP .cur/x LDZ ADD .test/x STZ #00 EQU
		?&no-x
	.test try-piece
		?&no-x
	( x changed )
	;sfx-vol/x LDA ;sfx/x play-sample
	.cur .test copy-piece
	#00 ;&freeze-timer STA
	&no-x

	( up button - hard drop )
	#10 button/pressed #00 EQU
		?&no-up
	update-ghost
	.cur .ghost copy-piece
		!&freeze
	&no-up

	( gravity )
	[ LIT &speed $1 ]
		?&gravity-tick
	( instant drop - like hard drop but don't freeze immediately )
	update-ghost
	.cur .ghost copy-piece
		!&run-freeze-timer
	( normal drop - descend 1 row every &speed frames )
	&gravity-tick
	[ LIT &tick $1 ] INC ,&speed LDR MOD DUP ,&tick STR #00 EQU
		?&inc-y

	( down button - soft drop )
	#20 button/held
		?&inc-y

	[ LIT &freeze-timer $1 ]
		?&run-freeze-timer
		!&update

	( try to increment y )
	&inc-y
		.test .cur copy-piece
		.test/y LDZ INC .test/y STZ
		.test try-piece
			?&run-freeze-timer ( blocked )
		( y changed )
		.cur .test copy-piece
		( ghost is unchanged )
		!&update
	( check if the freeze timer has expired )
	&run-freeze-timer
		,&freeze-timer LDR #1e GTH
			?&freeze
		,&freeze-timer LDR INC ,&freeze-timer STR
			!&update
	( freeze the current piece )
	&freeze
		;sfx-vol/freeze LDA ;sfx/freeze play-sample
		freeze-cur-piece
		#00 ,&freeze-timer STR
	( create a new piece )
	&make-new-piece
		#00 ;&hold-swapped STA
		new-piece
		( if the new piece doesn't fit, die )
		.cur try-piece
			?&die
		#00 ;&is-compact STA ( compact next frame )

	( update the current piece and ghost ) 
	&update
		update-ghost
		set-ghost-piece
		set-cur-piece
			!&draw

	&die
		init-game-over

	&draw
		draw-fg
BRK

@init-game-over
	;on-frame-game-over .Screen/vector DEO2
	#0280 ;on-frame-game-over/n STA2
	;sfx-vol/game-over LDA ;sfx/tetris #28 play-sample-note
JMP2r

@on-frame-game-over
	[ LIT2 &n $2 ] #0140 LTH2
		?&draw
	,&n LDR2 #0002 SUB2 ,&n STR2
	,&n LDR2 ;well ADD2
		DUP #07 MOD #10 ORA
		OVR INC #07 MOD #10 ORA
	#00
	,&n LDR2 #01d0 LTH2
	,&n LDR2 #01f0 GTH2
		EOR MUL2
	SWP2 STA2

	&draw
	draw-fg

	,&n LDR2 #01d0 GTH2
		?&done
	;pos/well-x LDA2 #0004 ADD2
	;pos/well-y LDA2 #004d ADD2 
	;strings/game-over #41 draw-str

	#08 button/pressed #00 EQU
		?&done
	init-game
	&done
BRK

@button
	[ LIT &state $1 ]
&vector ( -> )
	.Controller/button DEI DUP
	,&state STR
	[ LIT &debounce $1 ] AND ,&debounce STR
BRK
&held ( code -- held )
	DUP ,&state LDR AND EOR #00 EQU
JMP2r
&pressed ( code -- pressed )
	DUP ,&state LDR AND
		?&pressed-debounce
	&not-pressed
	POP #00
JMP2r
&pressed-debounce
	DUP
	,&debounce LDR AND
		?&not-pressed
	,&debounce LDR ORA ,&debounce STR
	#01
JMP2r

@init-pieces ( -- )
	#00 &loop new-piece INC DUP #06 LTH ?&loop POP
JMP2r

@init-level ( -- )
	#00 .score/level LDZ ;level-speeds ADD2 LDA
	;on-frame-game/speed STA
JMP2r

@init-well ( -- )
	;well &loop
		DUP2 #0fff AND2 #027f GTH2
			?&block
		DUP #0f AND #03 LTH
			?&block
		DUP #0f AND #0c GTH
			?&block
		&space #00
			!&store
		&block #80
		&store ROT ROT STAk ROT POP
		INC2 DUP2 #0fff AND2 #0300 LTH2
			?&loop
	POP2
JMP2r

@compact-well ( -- noop )
	#00 ;&lines STA
	#00 ,&line-sum STR
	;well #0002 ADD2 &loop
		INC2
		DUP2 #0fff AND2 #027f GTH2
			?&done
		DUP #0f AND #0c GTH
			?&eol
		( sum block high nibble )
		LDAk #04 SFT [ LIT &line-sum $1 ] ADD ,&line-sum STR
			!&loop
		&eol
			,&line-sum LDR #14 EQU
				?&hl-line
			,&line-sum LDR #28 EQU
				?&rem-line
			!&cont
			&hl-line 
				DUP2 #000a SUB2
				&hl-block
					DUP2 LDAk #8f AND #40 ORA ROT ROT STA
					INC2 GTH2k
						?&hl-block
				,&lines LDR INC ,&lines STR
				POP2
				!&cont
			&rem-line
				remove-well-line
				#00
				JMP2r
			&cont
			#00 ,&line-sum STR
			#0005 ADD2
				!&loop
	&done POP2

	,&lines LDR
		?&inc-score
	#01
JMP2r
	&inc-score
		,&lines LDR #04 EQU
			?&tetris
		;sfx-vol/line LDA ;sfx/line play-sample
			!&sfx-done
		&tetris
		;sfx-vol/tetris LDA ;sfx/tetris play-sample
		&sfx-done

		.score/lines LDZ2 ,&before-lines STR2

		#00 [ LIT &lines $1 ] .score/lines LDZ2 ADD2 .score/lines STZ2
		#00 ,&lines LDR DUP MUL .score/points LDZ2 ADD2 .score/points STZ2

		.score/level LDZ #1e EQU
			?&score-done
		[ LIT2 &before-lines $2 ] #000a DIV2 .score/lines LDZ2 #000a DIV2 EQU2
			?&score-done
		.score/level LDZ INC .score/level STZ
		init-level

		&score-done
		#00
	JMP2r

@remove-well-line ( well-addr-on-line -- )
	#fff0 AND2 #0010 ADD2 ( go to end of this line )
	&loop
		#0001 SUB2
		( read block from line above )
		DUP2k #0010 SUB2 LDA
		( store block in current line )
		ROT ROT STA
		( see if we're done )
		DUP2 #0fff AND2 #0010 GTH2
			?&loop
	POP2
JMP2r

@copy-piece ( dst src -- )
	SWPk INC SWP INC
	SWPk INC SWP INC
	SWPk INC SWP INC
	LDZ SWP STZ
	LDZ SWP STZ
	LDZ SWP STZ
	LDZ SWP STZ
JMP2r

@init-cur-pos ( -- )
	#06 .cur/x STZ
	#11 .cur/y STZ
	#00 .cur/rot STZ
JMP2r

@new-piece ( -- )
	init-cur-pos
	.next LDZ .cur/type STZ
	#00 &loop
		INCk .next ADD LDZ
		OVR .next ADD STZ
		INC DUP #04 LTH
			?&loop
	.next ADD
	next-piece-type SWP STZ
JMP2r

@next-piece-type
	[ LIT &len $1 ]
		?&pop
	( fill bag 0-6 )
	;&bag #00 &fill
		ROT ROT STAk
		INC2 ROT INC DUP #07 LTH
			?&fill
	,&len STR POP2
	( shuffle bag )
	;&bag #07 shuffle
	( pop type from bag )
	&pop
	,&len LDR #01 SUB DUP ,&len STR
	#00 SWP ;&bag ADD2 LDA
JMP2r
	[ &bag $7 ]

@set-piece ( piece val -- )
	,&val STR
	;&each !each-piece-block
&each ( well-offset -- )
	;well ADD2 [ LIT &val $1 ] ROT ROT STA
JMP2r

@set-cur-piece ( -- )
	.cur DUP LDZ #10 ORA !set-piece

@freeze-cur-piece ( -- )
	.cur DUP LDZ #20 ORA !set-piece

@set-ghost-piece ( -- )
	.ghost #07 !set-piece

@clear-piece ( -- )
	#00 !set-piece

( tries to move cur to test, reports whether it failed )
@try-piece ( piece -- blocked )
	#00 ,&blocked STR
	;&each each-piece-block
	,&blocked LDR
JMP2r
	&each ( well-offset -- )
		;well ADD2 LDA #e0 AND
		[ LIT &blocked $1 ] ORA ,&blocked STR
	JMP2r

( set ghost to cur, but at the bottom-most y point that fits )
@update-ghost
	.ghost .cur copy-piece
	&loop
		.ghost/y LDZ INC .ghost/y STZ
		.ghost try-piece #00 EQU 
			?&loop
	.ghost/y LDZ #01 SUB .ghost/y STZ
JMP2r

( try all the kicks for the test piece for the given rotation,
  leaves the test piece in a slot that fits )
@try-kick ( anti-clockwise -- blocked )
	#00 SWP #08 MUL
	#00 .test/type LDZ #00 EQU #40 MUL
	#00 .test/rot LDZ #10 MUL
	;kicks ADD2 ADD2 ADD2 ,&addr STR2
	#00 ,&offs STR
	&loop
		[ LIT2 &addr $2 ] #00 [ LIT &offs $1 ] ADD2 INCk2
		LDA .test/y LDZ ADD .test/y STZ
		LDA .test/x LDZ ADD .test/x STZ
		.test try-piece
			?&next
	#00 ( kick succeeded )
JMP2r
	&next ( reset and try next kick, if any left )
		.cur/x LDZ .test/x STZ 
		.cur/y LDZ .test/y STZ
		,&offs LDR #02 ADD DUP ,&offs STR #08 LTH
			?&loop
	#01 ( all kicks blocked )
JMP2r

( return the bits that define a 4x4 piece
  of the given type and rotation )
@piece-bits ( rot type -- piece-bits* )
	#30 SFT ( type ) 
	SWP #10 SFT ( rot )
	ADD #00 SWP
	;pieces ADD2 LDA2 ( piece16 )
JMP2r

@each-piece-block ( piece each-func* -- )
	,&each-func STR2
	LDZk ( type ) SWP INC LDZk ( rot )
	SWP INC LDZk ( x ) SWP INC LDZ ( y )
	#00 SWP #0010 MUL2 ROT #00 SWP ADD2 ,&offset STR2
	SWP piece-bits ;&each !each-piece-bits-block
&each
	DUP #0c AND #02 SFT #10 MUL
	SWP #03 AND ADD
	#00 SWP [ LIT2 &offset $2 ] ADD2
	[ LIT2 &each-func $2 ] JMP2

@each-piece-bits-block ( piece-bits* each-func* -- )
	,&each-func STR2 ,&piece-bits STR2
	( go backward from bottom-right, as we
	  consume piece-bits least-to-most significant )
	#0f &loop
		( test whether a piece block is at woffs )
		[ LIT2 &piece-bits $2 ] NIP #01 AND #01 NEQ ?&no
			DUP [ LIT2 &each-func $2 ] JSR2
		&no

		#01 SUB
		( shift piece-bits right )
		,&piece-bits LDR2 #01 SFT2 DUP2 ,&piece-bits STR2
		( continue if there are more blocks )
		#0000 GTH2
			?&loop
	POP
JMP2r

( draw game state )

@pos
	&well-x 0058
	&well-y 0030
	&well-w 0a
	&well-h 14

	&next-x 00b0
	&next-y 0030
	&next-w 06
	&next-h 0f

	&hold-x 00b0
	&hold-y 00b0
	&hold-w 06
	&hold-h 04

	&score-x 0020
	&score-y 0030
	&score-w 06
	&score-h 08

@draw-fg
	( clear )
	#0000 .Screen/x DEO2
	#0000 .Screen/y DEO2
	#c0 .Screen/pixel DEO

	draw-next
	draw-hold
	draw-score

	;on-frame-game/is-paused LDA
		?draw-pause

	( clear well background here,
	  because draw-pause may have painted it )
	;zero .Screen/addr DEO2
	;pos/well-x LDA2 .Screen/x DEO2
	;pos/well-y LDA2 .Screen/y DEO2
	;pos/well-w LDA
	;pos/well-h LDA
	#00 draw-sprite-rect

	draw-well
JMP2r

@draw-pause
	;bg/stripes .Screen/addr DEO2
	;pos/well-x LDA2 .Screen/x DEO2
	;pos/well-y LDA2 .Screen/y DEO2
	;pos/well-w LDA
	;pos/well-h LDA
	#03 draw-sprite-rect

	#006a #0080 ;strings/pause #41 draw-str
JMP2r

@draw-next
	#00 ,&n STR
	&loop
		;pos/next-x LDA2 #0008 ADD2
		;pos/next-y LDA2 #000b ADD2 #0015 #00 ,&n LDR MUL2 ADD2
		.next ,&n LDR ADD LDZ
		draw-piece
		[ LIT &n $1 ] INC DUP ,&n STR #05 LTH
			?&loop
JMP2r

@draw-hold
	.hold/ok LDZ #00 EQU
		?&done
	;pos/hold-x LDA2 #0008 ADD2
	;pos/hold-y LDA2 #0009 ADD2
	.hold/type LDZ
	draw-piece
	&done
JMP2r

( draw piece centered )
@draw-piece ( screen-x* screen-y* type -- )
	,&type STR 
	#00 ,&type LDR #00 NEQ #0004 MUL2 ADD2 ,&screen-y STR2
	#00 ,&type LDR DUP #00 NEQ SWP #03 NEQ AND #0004 MUL2 ADD2 ,&screen-x STR2
	#00 ,&type LDR piece-bits ;&each !each-piece-bits-block
&each
	DUP
	#03 AND #08 MUL #00 SWP [ LIT2 &screen-x $2 ] ADD2 .Screen/x DEO2
	#02 SFT #08 MUL #00 SWP [ LIT2 &screen-y $2 ] ADD2 .Screen/y DEO2
	[ LIT &type $1 ] #10 ORA ;draw-block JMP2

@draw-well ( -- )
	;pos/well-x LDA2 .Screen/x DEO2
	;pos/well-y LDA2 .Screen/y DEO2
	#01 .Screen/auto DEO
	;well #0142 ADD2 &loop
		INC2
		DUP2 #0fff AND2 #027f GTH2
			?&done
		DUP #0f AND #0c GTH
			?&eol
		DUP2 LDA draw-block
			!&loop
		&eol
			.Screen/x DEI2 #0050 SUB2 .Screen/x DEO2
			.Screen/y DEI2 #0008 ADD2 .Screen/y DEO2
			#0005 ADD2
			!&loop
	&done POP2
JMP2r

@draw-block ( block-type -- )
	DUP #77 AND
		?&block
	POP
	.Screen/x DEI2 #0008 ADD2 .Screen/x DEO2
JMP2r
	&block
		#07 AND #03 MUL #00 SWP ;piece-sprites ADD2
		INC2k INC2 LDA ROT ROT LDA2
		.Screen/addr DEO2
		.Screen/sprite DEO
	JMP2r

@draw-score ( -- )
	;pos/score-x LDA2 #0004 ADD2 
	;pos/score-y LDA2 #000e ADD2
	#00 .score/level LDZ
	#41
	draw-num
	;pos/score-x LDA2 #0004 ADD2 
	;pos/score-y LDA2 #0022 ADD2
	.score/lines LDZ2
	#41
	draw-num
	;pos/score-x LDA2 #0004 ADD2 
	;pos/score-y LDA2 #0036 ADD2
	.score/points LDZ2
	#41
	draw-num
JMP2r

( backgrounds )

@draw-bg ( -- )
	( background )
	;bg/grid-dot .Screen/addr DEO2
	#0000 .Screen/x DEO2
	#0000 .Screen/y DEO2
	#20 #20 #03 draw-sprite-rect

	( main field )
	;pos/well-x LDA2 .Screen/x DEO2
	;pos/well-y LDA2 .Screen/y DEO2
	;pos/well-w LDA
	;pos/well-h LDA
	#8a draw-box 

	( next piece )
	;pos/next-x LDA2 .Screen/x DEO2
	;pos/next-y LDA2 .Screen/y DEO2
	;pos/next-w LDA
	;pos/next-h LDA
	#8a draw-box

	;zero .Screen/addr DEO2
	;pos/next-x LDA2 .Screen/x DEO2
	;pos/next-y LDA2 .Screen/y DEO2
	;pos/next-w LDA
	;pos/next-h LDA
	#00 draw-sprite-rect

	;pos/next-x LDA2 #0008 ADD2
	;pos/next-y LDA2 #0003 ADD2 
	;strings/next #02 draw-str

	( hold piece )
	;pos/hold-x LDA2 .Screen/x DEO2
	;pos/hold-y LDA2 .Screen/y DEO2
	;pos/hold-w LDA
	;pos/hold-h LDA
	#8a draw-box

	;zero .Screen/addr DEO2
	;pos/hold-x LDA2 .Screen/x DEO2
	;pos/hold-y LDA2 .Screen/y DEO2
	;pos/hold-w LDA
	;pos/hold-h LDA
	#00 draw-sprite-rect

	;pos/hold-x LDA2 #0008 ADD2
	;pos/hold-y LDA2 #0003 ADD2 
	;strings/hold #02 draw-str

	( score )
	;pos/score-x LDA2 .Screen/x DEO2
	;pos/score-y LDA2 .Screen/y DEO2
	;pos/score-w LDA
	;pos/score-h LDA
	#8a draw-box

	;zero .Screen/addr DEO2
	;pos/score-x LDA2 .Screen/x DEO2
	;pos/score-y LDA2 .Screen/y DEO2
	;pos/score-w LDA
	;pos/score-h LDA
	#00 draw-sprite-rect

	;pos/score-x LDA2 #0004 ADD2
	;pos/score-y LDA2 #0004 ADD2 
	;strings/level #02 draw-str

	;pos/score-x LDA2 #0004 ADD2
	;pos/score-y LDA2 #0018 ADD2 
	;strings/lines #02 draw-str

	;pos/score-x LDA2 #0004 ADD2
	;pos/score-y LDA2 #002c ADD2 
	;strings/score #02 draw-str
JMP2r

@draw-box ( w h col -- )
	;&col STA #08 MUL ;&h STA #08 MUL ;&w STA

	.Screen/x DEI2 #0008 SUB2 .Screen/x DEO2
	.Screen/y DEI2 #0008 SUB2 .Screen/y DEO2

	( tl )
	#01 .Screen/auto DEO
	;border-chr/corner .Screen/addr DEO2
	#00 draw-box/put
	( top )
	;border-chr/edge-hor .Screen/addr DEO2
	#00 [ LIT &w $1 ] draw-box/putn
	( tr )
	#02 .Screen/auto DEO
	;border-chr/corner .Screen/addr DEO2
	#10 draw-box/put
	( right )
	;border-chr/edge-ver .Screen/addr DEO2
	#10 [ LIT &h $1 ] draw-box/putn
	( br )
	;border-chr/corner .Screen/addr DEO2
	#30 draw-box/put
	( left )
	.Screen/x DEI2 #00 ;&w LDA #08 ADD SUB2 .Screen/x DEO2
	.Screen/y DEI2 #00 ;&h LDA #08 ADD SUB2 .Screen/y DEO2
	;border-chr/edge-ver .Screen/addr DEO2
	#00 ;&h LDA draw-box/putn
	( bl )    
	#01 .Screen/auto DEO
	;border-chr/corner .Screen/addr DEO2
	#20 draw-box/put
	( bottom )
	;border-chr/edge-hor .Screen/addr DEO2
	#20 ;&w LDA draw-box/putn
JMP2r
	&put ( flags -- )
		[ LIT &col $1 ] ORA .Screen/sprite DEO
	JMP2r
	&putn ( n flags -- )
		&wloop OVR ,&put JSR #08 SUB DUP ?&wloop
		POP2
	JMP2r

@draw-sprite-rect ( w h col -- )
	;&col STA
	SWP DUP ,&w STR
	#00 SWP ROT #00 SWP MUL2
	#01 .Screen/auto DEO
	&loop
		#0001 SUB2
		[ LIT &col $1 ] .Screen/sprite DEO
		DUP2 #0000 EQU2
			?&done
		DUP2 #00 [ LIT &w $1 ] DIV2k MUL2 SUB2 NIP
			?&loop
		&line
		.Screen/x DEI2 #0008 #00 ;&w LDA MUL2 SUB2 .Screen/x DEO2
		.Screen/y DEI2 #0008 ADD2 .Screen/y DEO2
			!&loop
	&done POP2
JMP2r

@draw-image ( addr* x* y* -- )
	,&y STR2 ,&x STR2 
	LDAk ,&w STR INC LDAk ,&h STR INC
	.Screen/addr DEO2
	#05 .Screen/auto DEO
	[ LIT2 &y $2 ] .Screen/y DEO2
	[ LIT &h $1 ] &yloop
		[ LIT2 &x $2 ] .Screen/x DEO2
		[ LIT &w $1 ] &xloop
			#81 .Screen/sprite DEO
			#01 SUB DUP
				?&xloop
		POP
		.Screen/y DEI2 #0008 ADD2 .Screen/y DEO2
		#01 SUB DUP
			?&yloop
	POP
JMP2r

( text )

@draw-str ( x* y* text* color -- )
	#01 .Screen/auto DEO
	STH
	SWP2 .Screen/y DEO2
	SWP2 .Screen/x DEO2
	&loop
		LDAk #20 SUB #00 SWP #30 SFT2 ;font ADD2 .Screen/addr DEO2
		STHkr .Screen/sprite DEO
		INC2 LDAk ?&loop
	POP2
	STHr POP
	#00 .Screen/auto DEO
JMP2r

@draw-num ( *x *y num* color -- )
	#01 .Screen/auto DEO
	STH
	SWP2 .Screen/y DEO2
	SWP2 .Screen/x DEO2
	#00 ,&visible STR
	#2710 DIV2k DUP2 NIP ,&digit JSR MUL2 SUB2
	#03e8 DIV2k DUP2 NIP ,&digit JSR MUL2 SUB2
	#0064 DIV2k DUP2 NIP ,&digit JSR MUL2 SUB2 NIP
	#0a DIVk DUP ,&digit JSR MUL SUB
	,&draw-digit JSR
	STHr POP
	#00 .Screen/auto DEO
JMP2r
	&digit ( chr -- )
		DUP [ LIT &visible $1 ] ORA
			?&draw-digit
		POP #0000
			!&draw
		&draw-digit
		#10 ADD #00 SWP #30 SFT2 
		#01 ,&visible STR
		&draw
		;font ADD2 .Screen/addr DEO2
		OVR2r NIPr STHr .Screen/sprite DEO
	JMP2r

( audio )

@play-sample ( vol addr* -- )
	#3c play-sample-note
JMP2r

@play-sample-note ( vol addr* note -- )
	,&note STR
	DUP2 LDA2 .Audio0/length DEO2
	INC2 INC2 .Audio0/addr DEO2
	.Audio0/volume DEO
	#00f0 .Audio0/adsr DEO2
	[ LIT &note $1 ] #80 ORA .Audio0/pitch DEO
JMP2r

( prng )

@init-prng ( -- )
	#00 .DateTime/second DEI
	#00 .DateTime/minute DEI #60 SFT2 EOR2
	#00 .DateTime/hour DEI #c0 SFT2 EOR2 ,prng/x STR2
	#00 .DateTime/hour DEI #04 SFT2
	#00 .DateTime/day DEI #10 SFT2 EOR2
	#00 .DateTime/month DEI #60 SFT2 EOR2
		.DateTime/year DEI2 #a0 SFT2 EOR2 ,prng/y STR2
JMP2r

@prng ( -- number* )
	LIT2 &x $2
	DUP2 #50 SFT2 EOR2
	DUP2 #03 SFT2 EOR2
	LIT2 &y $2 DUP2 ,&x STR2
	DUP2 #01 SFT2 EOR2 EOR2
	,&y STR2k POP
JMP2r

@randn ( max -- num )
        #00 &mask-loop
                #10 SFT INC
                DUP2 GTH
                        ?&mask-loop
        ,&mask STR
        DUP &loop POP
                prng EOR [ LIT &mask $1 ] AND
                DUP2 LTH
                        ?&loop
        NIP
JMP2r

@shuffle ( addr* len -- )
        ROT ROT ,&a STR2
        &loop
                #01 SUB
                DUP randn #00 SWP ;&a LDA2 ADD2 ;&b STA2
                [ LIT2 &a $2 ] LDA [ LIT2 &b $2 ] LDA
                ;&a LDA2 STA ;&b LDA2 STA
                ;&a LDA2 INC2 ;&a STA2
                DUP #01 GTH
                        ?&loop
        POP
JMP2r

( game data )

@theme
	&set ( addr* -- )
		INC2k INC2 INC2k INC2
		LDA2 .System/b DEO2
		LDA2 .System/g DEO2
		LDA2 .System/r DEO2 
	JMP2r
	&title   f0a8 f0a8 f0a8
	&mono    c359 c359 d46a
	&reverse 2d94 2d94 3ea5
	&cga     0f5a 0ff0 0ffa
	&cga-ish 0ea5 0cf2 09d9 

@sfx-vol
	&x 99
	&rot aa
	&hold ff
	&freeze ff
	&line ff
	&tetris ff
	&game-over ff

@piece-sprites ( addr* color )
	( i ) =blocks-icn/a c3
	( s ) =blocks-icn/b c3
	( l ) =blocks-icn/c c3
	( b ) =blocks-icn/d c3
	( t ) =blocks-icn/a c1
	( z ) =blocks-icn/b c1
	( j ) =blocks-icn/c c1
	( ghost ) =ghost-icn 41

@pieces
	( i ) 0f00 2222 00f0 4444 
	( s ) 6c00 4620 06c0 8c40
	( l ) 2e00 4460 0e80 c440
	( b ) 6600 6600 6600 6600
	( t ) 4e00 4640 0e40 4c40
	( z ) c600 2640 0c60 4c80
	( j ) 8e00 6440 0e20 44c0
@kicks
	( j, l, t, s, z )
	( 3>>0 ) ff00 ff01 00fe fffe
	( 1>>0 ) 0100 0101 00fe 01fe
	( 0>>1 ) ff00 ffff 0002 ff02
	( 2>>1 ) ff00 ffff 0002 ff02
	( 1>>2 ) 0100 0101 00fe 01fe
	( 3>>2 ) ff00 ff01 00fe fffe
	( 2>>3 ) 0100 01ff 0002 0102
	( 0>>3 ) 0100 01ff 0002 0102
	( i )
	( 3>>0 ) 0100 fe00 0102 feff
	( 1>>0 ) 0200 ff00 02ff ff02
	( 0>>1 ) fe00 0100 fe01 01fe
	( 2>>1 ) 0100 fe00 0102 feff
	( 1>>2 ) ff00 0200 fffe 0201
	( 3>>2 ) fe00 0100 fe01 01fe
	( 2>>3 ) 0200 ff00 02ff ff02
	( 0>>3 ) ff00 0200 fffe 0201

@level-speeds
	35 31 2d 29 25 21 1c 16 11 0b
	0a 09 08 07 07 05 05 04 04 03
	03 03 02 02 02 01 01 01 01 01
	00

@strings
	&level "LEVEL $1
	&lines "LINES $1
	&score "SCORE $1
	&next "NEXT $1
	&hold "HOLD $1
	&game-over "GAME 20 "OVER $1
	&pause "PAUSED $1
	&start "PRESS 20 "START $1

( sprites )

@zero
	0000 0000 0000 0000
	0000 0000 0000 0000
@blocks-icn
	&a
	007e 7e7e 7e7e 7e00
	ff81 8181 8181 81ff
	&b
	007e 7e66 667e 7e00
	ff81 bdbd bdbd 81ff
	&c
	007e 425a 5a42 7e00
	ff81 bda5 a5bd 81ff
	&d
	007e 7e7e 7e7e 7e00
	ffab d5ab d5ab d5ff
@ghost-icn
	aa01 8001 8001 8055
	0000 0000 0000 0000
@border-chr
	&corner
	0000 0000 0000 0001
	0000 0000 0000 0000
	&edge-ver
	0101 0101 0101 0101
	0202 0202 0202 0202
	&edge-hor
	0000 0000 0000 00ff
	0000 0000 0000 ff00
@bg
	&stripes
	030c 30c0 030c 30c0
	e38f 3ef8 e38f 3ef8
	&grid-dot
	0010 0010 aa10 0010
	0000 0000 0000 0000

( pre-calculated tables )

@costab
	fe fd fd fd fd fd fc fc fb fa fa f9 f8 f7 f6 f5
	f4 f3 f1 f0 ef ed eb ea e8 e6 e5 e3 e1 df dd da
	d8 d6 d4 d1 cf cd ca c8 c5 c2 c0 bd ba b8 b5 b2
	af ac a9 a6 a3 a0 9d 9a 97 94 91 8e 8b 88 85 82
	7f 7b 78 75 72 6f 6c 69 66 63 60 5d 5a 57 54 51
	4e 4b 48 45 43 40 3d 3b 38 35 33 30 2e 2c 29 27
	25 23 20 1e 1c 1a 18 17 15 13 12 10 0e 0d 0c 0a
	09 08 07 06 05 04 03 03 02 01 01 00 00 00 00 00
	00 00 00 00 00 00 01 01 02 03 03 04 05 06 07 08
	09 0a 0c 0d 0e 10 12 13 15 17 18 1a 1c 1e 20 23
	25 27 29 2c 2e 30 33 35 38 3b 3d 40 43 45 48 4b
	4e 51 54 57 5a 5d 60 63 66 69 6c 6f 72 75 78 7b
	7e 82 85 88 8b 8e 91 94 97 9a 9d a0 a3 a6 a9 ac
	af b2 b5 b8 ba bd c0 c2 c5 c8 ca cd cf d1 d4 d6
	d8 da dd df e1 e3 e5 e6 e8 ea eb ed ef f0 f1 f3
	f4 f5 f6 f7 f8 f9 fa fa fb fc fc fd fd fd fd fd

~sfx/sfx.tal
~gfx/gfx.tal
~font.tal
~music.tal

( game state )

|f000

@well $300
